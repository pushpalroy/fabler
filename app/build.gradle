apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.jakewharton.butterknife'

repositories {
    mavenLocal()
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

android {
    signingConfigs {
        config {
            keyAlias 'key0'
            keyPassword 'Fabler@nano'
            storeFile file('keystore/Keystore.jks')
            storePassword 'Fabler@udacity'
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.techradge.fabler"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField("String", "BASE_URL", "\"http://www.mocky.io/v2\"")
            buildConfigField("String", "API_KEY", "\"ABCXYZ123TEST\"")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            buildConfigField("String", "BASE_URL", "\"http://www.mocky.io/v2\"")
            buildConfigField("String", "API_KEY", "\"ABCXYZ123TEST\"")
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Robolectric support
    testOptions {
        //unitTests.includeAndroidResources = true
    }

    dataBinding {
        enabled true
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support Libraries

    // noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    // Constraint Layout
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Multidexing
    implementation 'androidx.multidex:multidex:2.0.1'

    // Dependency injection
    implementation "com.google.dagger:dagger:$dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger2Version"
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation 'javax.inject:javax.inject:1'

    // logging
    implementation "com.jakewharton.timber:timber:$timber"

    // Guava
    implementation "com.google.guava:guava:$guavaVersion"

    // network
    implementation "com.amitshekhar.android:rx2-android-networking:$rx2FastAndroidNetworking"

    // Room - Database
    implementation 'androidx.room:room-runtime:2.1.0'
    annotationProcessor 'androidx.room:room-compiler:2.1.0'

    // Architecture components: Lifecycle Components (ViewModel and LiveData)
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // ButterKnife
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // Firebase Real-time Database
    implementation 'com.google.firebase:firebase-database:16.0.1'

    // Firebase FirebaseUI Auth only
    implementation 'com.firebaseui:firebase-ui-auth:4.1.0'

    // Support for Facebook login
    implementation 'com.facebook.android:facebook-android-sdk:4.41.0'

    // Glide: Image Loading
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // Circular ImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Loader
    implementation 'com.victor:lib:1.0.4'

    // Custom Toggle Button
    implementation 'com.github.jd-alexander:LikeButton:0.2.3'

    // Editor Library
    implementation project(':fablededitor')

    // *************** Testing *************** //

    // ***** AndroidX Test ***** //

    // Core library, Robolectric environment
    testImplementation 'androidx.test:core:1.2.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    testImplementation 'com.google.truth:truth:0.42'

    // Robolectric
    testImplementation "org.robolectric:robolectric:4.0.2"

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
